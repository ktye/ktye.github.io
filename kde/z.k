``x`y`z`k`l`a`b`while`"rf."`"rz."`"uqs."`"uqf."`"gdt."`"lin."`"odo."`"grp."
`"x.":{,/+"0123456789abcdef"@(x%16;16/x:256/256+x)}
`"t.":`45
`"p.":`46
`"c.":(`46)[`c;]
`"i.":(`46)[`i;]
`"s.":(`46)[`s;]
`"f.":(`46)[`f;]
`"z.":(`46)[`z;]
`"uqs.":{x@&~0~':x:^x}
`"uqf.":{x@&(!#x)=x?x}
`"gdt.":{[t;g](!#t)($[g;{x@>y x};{x@<y x}])/|.t}
`"odo.":{{y@(#y)/!x}/:[*/x;&'x#'|*\-1_1,|x]}
`"grp.":{(x@*'g)!g:(&~x~':x i)^i:<x}
abs:`32;sin:`44;cos:`39;find:`31;fill:`38;imag:`33;conj:`34;angle:`35;exp:`42;log:`43
`"pad.":{(|/#'x)#'x}
`"l.":{
kt:{[x;y;k;T]x:$[`T~@x;T[x;k];`pad("";"-"),$x];(x,'"|"),'T[y;k]}
d:{[x;k;kt;T]r:!x;x:.x;$[`T~@x;kt[r;x;k;T];,'[,'[`pad(k'r);"|"];k'x]]}
T:{[x;k]$[`L':@'.x;,k x;(,*x),(,(#*x)#"-"),1_x:" "/:'+`pad@'$(!x),'.x]}
t:@x;k:`kxy 1
dd:("";,"..")20<#x:$[(@x)':`L`D`T;x;x~*x;x;[t:`L;,x]]
x:$[x~*x;x;(20&#x)#x]
$[`D~t;d[x;k;kt;T];`T~t;T[x;k];x~*x;,k x;k'x],dd}
`"str.":{q:{c,("\\"/:(0,i)^@[x;i;(qs!"tnr\"\\")x i:&x':qs:"\t\n\r\"\\"]),c:_34}
$[|/x':"\t\n\r"__!31;"0x",`x@x;q x]}
`"kxy.":{
a:{t:@x;x:$x;$[`c~t;`str x;`s~t;"`",x;x]}
d:{[x;k]r:"!",k@.x;n:#!x;x:k@!x;$[(n<2)|(@.x)':`D`T;"(",x,")";x],r}
v:{[x;k;m;n]m*:(.`".kstm")t:@x; dd:("";"..")m<#x;x:(m&#x)#x
x:$[`L~t;k'x;`C~t;x;$x]
x:$[`C~t;`str x;`S~t;c,(c:"`")/:x;`L~t;$[1~n;*x;"(",(";"/:x),")"];" "/:x]
(("";",")(1~n)),x,dd}
t:@y;n:#y;k:`kxy x;m:x
$[`T~t;"+",d[+y;k];`D~t;d[y;k];n~0;(.`".kst0")@t;y~*y;a y;v[y;k;m;n]]}
`".kst0":`C`I`S`F`Z`L!(c,c:_34;"!0";"0#`";"0#0.";"0#0a";"()")
`".kstm":`C`I`S`F`Z`L!100 30 30 20 10 20
`"k.":`kxy 1000000
`"rf.": {.5+(x?0)%4294967295.}
`"rf1.":{.5+(1.+x?0)%4294967295.}        
`"rz.": {(%-2*log `rf1 x)@360.*`rf x}
`"lin.":{$[`L~@z;(.`"lin.")[x;y]'z;[dx:0.+1_-':x;dy:0.+1_-':y;b:(-2+#x)&0|x'z;(y b)+(dy b)*(z-x b)%dx b]]}
