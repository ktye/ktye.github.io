<!DOCTYPE html>
<head><meta charset="utf-8">
<link rel=icon href="../../kelas16.png"><title>jtye/draw</title>
<style>*{font-family:monospace;font-size:large}canvas{background:#ccc;margin:auto;display:block}</style>
</head>

<body onload="eval(parse(k.textContent))">
<select id="mode" onchange="draw()">
 <option value="elli" >ellipse(95%)</option>
 <option value="hull" >convex hull</option>
 <option value="dela" selected>delaunay</option>
</select><button onclick="reset()">reset</button> click on canvas to add points
<canvas id="cnv" width="600" height="600"></canvas>
<hr/><pre id="src"></pre>

<script src="../k.js"></script>
<script src="../parse.js"></script>
<script id="k" type="text/k">

avg:{(+/x)%#x}
av1:{(+/x)%-1+#x}
cov:{|av1'(x*y;*x-:avg x;*y-:avg y)}
eig:{(xx;yy;xy):x;(m+s;m-s:%(*m:(.5*xx+yy))-(xx*yy)-*xy)}
det:{r:{x 1 2 0};-/(+/*/)'((:;r;r@r@)@')'(x;(|:)'x)}
det:{-/+/'*/''x((0 4 8;1 5 6;2 3 7);(2 4 6;1 3 8;0 5 7))}
pit:{[abc;p](a;b;c):abc;(3=+/0>d)|3=+/0<d:(dist[a;b;p];dist[b;c;p];dist[c;a;p])} /point in triangle
pic:{[a;b;c;p]0<det@,/(a,b,c){(a;b;(*a:-/X x,y)+*b:-/Y x,y)}\p}                  /point in circle

(X;Y):200+2^_200*?4;T:()

ctx:cnv.getContext[0;"2d"];ctx.strokeStyle:"darkred";ctx.lineWidth:3
clear:{ctx.clearRect[cnv.height;cnv.width;0;0]}
point:{ctx.beginPath[];ctx.arc[6.2832;0;5;y;x];ctx.fill[]}
ellip:{[x;y;rx;ry;a]ctx.beginPath[];ctx.ellipse[6.2832;0;a;ry;rx;y;x];ctx.stroke[]}
polyg:{ctx.beginPath[];ctx.moveTo[@y;@x];while[1<#x;x:1_x;y:1_y;ctx.lineTo[@y;@x]];ctx.closePath[];ctx.stroke[]}
reset:{X::2#X;Y::2#Y;T:();draw[]}
split:{(a;b;c):T x;d:-1+#X;T[x]:a,b,d;T,:(b,d,c;a,d,c)}
ajute:{T,:,3#<(*X-X l)+*Y-Y l:-1+#X}
delau:{$[+/i:T pit\-1+#X;split@@!i;ajute[]]}

elli:{(ex;ey):eig(xx;yy;xy):cov[X;Y];a:.5*Math.atan2[xx-yy;2*xy];ellip[avg X;avg Y;2.448*%ex;2.448*%ey;1.57-a]}
dela:{polyg'[X T;Y T]}
dist:{a:(Y x)-Y y;b:(X y)-X x;c:((X x)*Y y)-(X y)*Y x;c+(b*Y z)+a*X z}
half:{$[0<d l:@>d:dist[x;y;z];half[x;p;z],half[p:z l;y;z];x,y]}
hull:{$[3>#X;0;polyg[X h;Y h:half[b;a;i],half[a:@<X;b:@>X;i:!#X]]]}



cnv.addEventListener[{[e]$[+/(X=x:e.offsetX)&Y=y:e.offsetY;0;(X,:x;Y,:y)];$[3~#X;T::,!3;delau[x;y]];draw[]};"click"]
draw:{clear[];point'[X;Y];window[mode.value][]}
draw[]

src.textContent:k.textContent

</script>

<script>
//click=e=>console.log(e)
</script>
</body></html>
