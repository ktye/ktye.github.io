NB. Copyright (c) 1989 1990, Roger K.W. Hui & K.E. Iverson
NB. Copyright (c) 1990-1992, Iverson Software Inc.
NB. All Rights Reserved.


NB. !y ------------------------------------------------------------------

fac =. 1:`(* $:@<:) @. * " 0

(! -: fac) i.10

1 1 -: ! 0 1
_   -: ! 300
_   -: ! _1
__  -: ! _2

(!3.5)  -: 3.5 *!2.5
(!12.2) -: 12.2*!11.2
(!x) -: x*!x-1 [ x=.0.1*>:?20$100

1e_8 > | 0.8862269254 - !  0.5
1e_8 > | 1.2254167024 - ! _0.25
1e_8 > | 3.6256099082 - ! _0.75


NB. !y Stirling's approximation, Abramowitz & Stegun, 6.1.37 ------------

sbase    =. %:@o.@+: *  %&(^1) ^ ]
scorr    =. #.&(|.(%1 12 288),(_139%51840),(_571%2488320))@%"0
stirling =. sbase * scorr

f =. stirling |@-.@% !

*./ 1e_8 > f 10  +i.10
*./ 1e_8 > f 10.5+i.10
*./ 1e_8 > f     10 20 30 40 50 60 70 80 100 120 140 160
*./ 1e_8 > f 0.5+10 20 30 40 50 60 70 80 100 120 140 160


NB. x!y -----------------------------------------------------------------

case =. 2&#. @ (0&>*.(=<.)) @ ([,],-~)
f000 =. !@] % !@[ * !@-~
f001 =. 0:
f010 =. 'domain error'"0
f011 =. _1&^@[ * [ ! (->:)
f100 =. 0:
f101 =. 'impossible'"0
f110 =. _1&^@-~ * !&|&>:~
f111 =. 0:
f    =. f000`f001`f010`f011`f100`f101`f110`f111 @. case " 0

a =. _10+i.21
b =. _10+i.11
c =. i.16
a (!/ -: f/) b
a (!/ -: f/) c

(a%3) (!/ -: f/) c
(a%2) (!/ -: f/) c%2
(a%2) (!/ -: f/) c%3
(a%2) (!/ -: f/) c%o._1
a     (!/ -: f/) c%_4

bc =. (0&, + ,&0)@]^:([ > <:@#@])&(,1)
(!/~i.16) -: |:bc"0 i.16

pascal0 =. '|: (0&,+,&0)^:(i.y.) ,1' : ''
(!/~@i. -: pascal0) 32

pascal1 =. '+/\@(|.!.0)^:(i.y.) y.#1' : ''
(!/~@i. -: pascal1) 32

C =. <:`($:&<: + ($:<:))@.(< *. *@[)"0
(!/~@i. -: C/~@i.) 12

*./ 1 = 0 ! 0.5*_10+i.21
*./ 1 = !~  0.5*_10+i.21


NB.  x!y all size x combinations of i.y ---------------------------------

x=.0 0$''
x=.x, 't=.x. comb y.'
x=.x, 'z=.$0'
x=.x, 'z=.z, ($t) -: (x.!y.),x.'
x=.x, 'z=.z, *./,t e.i.y.'
x=.x, 'z=.z, *./x.=#@~."1 t'
x=.x, 'z=.z, (i.x.!y.) -: /:t'
x=.x, 'z=.z, (i.x.)-:"1 /:"1 t'
x=.x, '*./z'
f =. '':x

start =. i.@-.@-
count =. <:@[ ! <:@[ + |.@start
index =. ;@:((i.-])&.>)
recur =. (count#start) ,. (index@count{comb&.<:)
test  =. *@[ *. <
basis =. i.@(<: , [)
comb  =. basis`recur @. test

f/ +/\?5 7
4 f 4
0 f 4
0 f 0

seed =. *@[ i.@{ 1 0&,:@(,&1)@>:@(-~)
em   =. >:@{:@$
en   =. 2&+@{:@{:
g    =. em (count#start) en
h    =. em (index@count) en
grow =. g ,. h { >:
comb =. [ grow@]^:(>{:@$) seed

f/ +/\?5 7
4 f 4
0 f 4
0 f 0

t =. 0 0$''
t =. t,'$.=.((0=x.)#1),(0<x.)#2 3 4'
t =. t,'1 0$0'
t =. t,'k=.i.>:y.-x.'
t =. t,'c=.(<:x.)!(<:x.)+|.k'
t =. t,'(c#k),"0 1(->:|.;i.&.>|.c){x. comb&.<:y.'
comb =. '':t

f/ +/\?5 7
4 f 4
0 f 4
0 f 0
