NB. Copyright (c) 1989 1990, Roger K.W. Hui & K.E. Iverson
NB. Copyright (c) 1990-1992, Iverson Software Inc.
NB. All Rights Reserved.


NB. |y ------------------------------------------------------------------

type    =. 3!:0
complex =. 16&=@type
mag     =. (>.-)`(%:@*+)@.complex
f       =. | -: mag

NB. Boolean
0 1 -: | 0 1
t -: | t=.1=?2 3 4 5$2
f 1=?2 3 4$2

NB. integer
f _1e5+?25 2 2 2$2e5
3 2 1 0 1 2 3 -: | _3 _2 _1 0 1 2 3
0 -: 2147483648 - | _2147483648

NB. floating point
f o._1e5+?2 3 4 5$2e5

NB. complex
f r._1e5+?2 3 4$2e5
f j./_1e5+?2 3 4$2e5
5 5 5 5 -: |3j4 3j_4 _3j4 _3j_4


NB. x|y -----------------------------------------------------------------

test =. '' : ('r=.x.|y.'; '*./,((*r)e.0,*x.)*.(0=x.)+.(|r)<|x.')

NB. Boolean
(2 2$0 1 0 0) -: |/~0 1

NB. integer
(_50+?200$100) test _50+?200$100
(13$0  1  2) -:  3|_6 _5 _4 _3 _2 _1 0 1 2 3 4 5 6
(13$0 _2 _1) -: _3|_6 _5 _4 _3 _2 _1 0 1 2 3 4 5 6
t -: 0|t=._25+?400$50

NB. floating point
(  _50+?200$100) test o._50+?200$100
(o._50+?200$100) test   _50+?200$100
(o._50+?200$100) test o._50+?200$100
