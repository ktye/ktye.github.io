NB. Copyright (c) 1989 1990, Roger K.W. Hui & K.E. Iverson
NB. Copyright (c) 1990-1992, Iverson Software Inc.
NB. All Rights Reserved.


NB. %. ------------------------------------------------------------------

X  =. +/ . *
en =. 1&{@(,&1 1)@$

t=.0 0$''
t=.t, 'n  =. en y.'
t=.t, 'm  =. >.-: n'
t=.t, 'a0 =. m{."1 y.'
t=.t, 'a1 =. m}."1 y.'
t=.t, 't0 =. qr a0'
t=.t, 'q0 =. >@{. t0'
t=.t, 'r0 =. >@{: t0'
t=.t, 'c  =. (+|:q0) X a1'
t=.t, 't1 =. qr a1 - q0 X c'
t=.t, 'q1 =. >@{. t1'
t=.t, 'r1 =. >@{: t1'
t=.t, '(q0,.q1);(r0,.c),(-n){."1 r1'
q2   =. t : ''
norm =. (%:@X +)@,
qr   =. q2`((% ;&,. ,~@en@[ $ ]) norm) @. (1&>:@en)

em   =. >.@-:@#                    NB.  m  =. >.-:#y.
mm   =. ,~@em                      NB.  mm =. m,m
ai   =. rinv@(mm {. ])             NB.  ai =. rinv mm{.y.
di   =. rinv@(mm }. ])             NB.  di =. rinv mm}.y.
bee  =. (em , em - #) {. ]         NB.  b  =. (m,m-n){.y.
bx   =. -@(ai X bee X di)          NB.  bx =. - ai X b X di
r4   =. (ai,.bx) , (-@# {."1 di)   NB.  (ai,.bx),(-n){."1 di
rinv =. r4`% @. (1&>:@#)

minv =. (|.@$ ($,) (rinv@] X +@|:@[)&>/@qr) " 2
mdiv =. (%.@] +/ . * [) " _ 2

id=.=&i.&#

4  9 -: $%.1=?9 4$2
6 10 -: $%.?10 6$100
7 11 -: $%.?11 7$100
5 7  -: $%.j./?2 7 5$100

1       -: %.1
(%4)    -: %.4
(%3.5)  -: %.3.5
(%4j_5) -: %.4j_5

(%. -: minv) 1
(%. -: minv) ?12369
(%. -: minv) o.?1721
(%. -: minv) j./?100 2001

(b-:minv a) *. 1e_12>>./|,(id a)-a X b=.%.a=._50000+?10 10$10000
(b-:minv a) *. 1e_12>>./|,(id a)-a X b=.%.a=.0.01*_4000+?7 7$10000
(b-:minv a) *. 1e_12>>./|,(id a)-a X b=.%.a=.j./?2 8 8$1300

(b-:minv a) *. (1=+/a*b) *. (+/a*+a)-:%+/b*+b =.%.a=.1=?7$2
(b-:minv a) *. (1=+/a*b) *. (+/a*+a)-:%+/b*+b =.%.a=._10+?17$20
(b-:minv a) *. (1=+/a*b) *. (+/a*+a)-:%+/b*+b =.%.a=.0.1*_10+?13$20
(b-:minv a) *. (1=+/a*b) *. (+/a*+a)-:%+/b*+b =.%.a=.r.?23$20

x -: %. x=.=i.1
x -: %. x=.=i.2
x -: %. x=.=i.4
x -: %. x=.=i.9

f =. (%@[ * ]) -: %.@:*

4        f =i.1
3        f =i.2
_912     f =i.15

_6.9     f =i.1
5.79     f =i.2
_13.9    f =i.9

0j1      f =i.1
3j4      f =i.2
123j_7.9 f =i.16

(i.0 0 ) -: %.   i.0 0
(i.0 10) -: %.   i.10 0
(i.0)    -: %.   i.0

(i.0 0 ) -: minv i.0 0
(i.0 10) -: minv i.10 0
(i.0)    -: minv i.0

(i.0 0 ) -: (i.0 0)     %.   i.0 0
(i.0 10) -: (i.0 10)    %.   i.0 0
(i.0)    -: (?10$100)   %.   i.10 0
(i.0 3)  -: (?10 3$100) %.   i.10 0

(i.0 0 ) -: (i.0 0)     mdiv i.0 0
(i.0 10) -: (i.0 10)    mdiv i.0 0
(i.0)    -: (?10$100)   mdiv i.10 0
(i.0 3)  -: (?10 3$100) mdiv i.10 0

(    ?10$100)   (%. -: mdiv) _50000+?10 10$112300
(0.1*?10$100)   (%. -: mdiv) 0.0231*_4000+?10 10$12200
(j./?2 10$100)  (%. -: mdiv) j./0.0231*_4000+?2 10 10$12200

(?10 10$100)    (%. -: mdiv) ?10 10$100
(0.1*?7 7$100)  (%. -: mdiv) 0.231*_40+?7 7$100
(j./?2 9 9$100) (%. -: mdiv) r./0.231*_40+?2 9 9$100
