NB. Copyright (c) 1989 1990, Roger K.W. Hui & K.E. Iverson
NB. Copyright (c) 1990-1992, Iverson Software Inc.
NB. All Rights Reserved.


NB. a. ------------------------------------------------------------------

type =. 3!:0

2           -: type a.
1           -: #$a.
256         -: #a.
(i.256)     -: i.~a.
32 48 65 97 -: a.i.' 0Aa'


NB. A. ------------------------------------------------------------------

type  =. 3!:0
boxed =. 32&=@type
pind  =. ]`]`+@.(*@])"0
pfill =. [ ((i.@[-.]) , ]) pind

ord   =. >:@(>./)
base  =. >:@i.@-@#
rfd   =. +/@({.>}.)\.
dfr   =. /:^:2@,/

adot1 =. (base #. rfd)@((ord pfill ])`C.@.boxed) " 1
adot2 =. dfr@(base@] #: [) { ]

(A. -: adot1) 7?12
(A. -: adot1) (1=?7$3) <;.1 (7?12)

(?!5) (A. -: adot2) 'xyzab'
(?!5) (A. -: adot2) r.i.5 2

5 -: A.0 3 2 1
5 -: A.3 2 1
5 -: A.0;2;3 1
5 -: A.<3 1

(_1 A. y) -: |. y=.1=?300 2$2
(_1 A. y) -: |. y=.(?400$#a.){a.
(_1 A. y) -: |. y=.?300$2000
(_1 A. y) -: |. y=.o.?400 2$100
(_1 A. y) -: |. y=.^0j1*?400 2$100
(_1 A. y) -: |. y=.^0j1*?400 2$100
(_1 A. y) -: |. y=.(?400$#y){y=.;:'^0j1*?400 2$100'

(0 A. y) -: ,y=.1=?2
(0 A. y) -: ,y=.(?#a.){a.
(0 A. y) -: ,y=.?20000
(0 A. y) -: ,y=.o.?20000
(0 A. y) -: ,y=.^0j1*?2000
(0 A. y) -: ,y=.<^0j1*?2000

(0 A. y) -: y=.i.0 4 5
(0 A. y) -: y=.0 5$a.
(0 A. y) -: y=.0$<1234


NB. A. encore -----------------------------------------------------------

f =. i.@! A. i.

perm =. (,:@$&0)`(i. ,/@(,"1"_1) $:@<: {"2 1 -."1 0~@i.) @. (1&<)

j=.?~!5
p=.j{perm 5
j -: A.p
j -: A.@C.p
p -: j A.i.5

(f -: perm) ?6

seed =. 1 0&$
ni   =. i.@>:@{:@$
grow =. ni ,/@(,"1"_1) ] {"2 1 -."1 0~@ni
perm =. [ grow@]^:(>{:@$) seed

(f -: perm) ?6
(f n) -: grow^:n seed 0 [ n=.?6

t =. 0 0$''
t =. t,'$.=.1+1<y.'
t =. t,',:y.$0'
t =. t,',/(i.y.),"1"_1(1[\.i.y.){~"1 2 perm<:y.'
perm =. t : ''

(f -: perm) ?6
