NB. Copyright (c) 1989 1990, Roger K.W. Hui & K.E. Iverson
NB. Copyright (c) 1990-1992, Iverson Software Inc.
NB. All Rights Reserved.


NB. f/. f\  f\. models --------------------------------------------------

id     =. [.+

en     =. #@]
em     =. (en >.@% 1&>.@|@[)`(en 0&>.@>:@- [) @. (0&<:@[)
kay    =. en`em @. (0&<@[)
omask  =. (em,en) $ ($&0@|@[ , $&1@kay)

base   =. 1&>.@-@[ * i.@em
iind   =. base ,. |@[ <. en - base
seg    =. ((+i.)/@[ { ])"1 _

infix  =. (@seg) (iind `) (`]) \ ("0 _)
outfix =. (@#  ) (omask`) (`]) \ ("0 _)
prefix =. (@{.) (>:@,.@i.@#`) (`]) \
suffix =. (@}.) (   ,.@i.@#`) (`]) \

key    =. (@#) (=@[`) (`]) \

osub   =. >@]`(>@[ >@:{ ]) @. (*@#@])
oind   =. (+/&i./ </.&, i.)@(2&{.)@(,&1 1)@$
ob     =. (@(osub"0 1)) (oind`) (`(,@(<"_2))) \

bs     =. id (prefix : infix )
bsd    =. id (suffix : outfix)
sd     =. id (ob     : key   )


NB.  f/.y ---------------------------------------------------------------

NB. Boolean
a=.1=?10 5$2
(</.   -: < ob) a
(</.   -: < ob) ,a
(]/.   -: ] ob) a
(+.//. -: +./ ob) a
(</.   -: < ob) 1
(</.   -: < ob) 0$1

NB. literal
a=.a.{~32+?10 5 3$95
(</. -: < ob) a
(</. -: < ob) ,a
(]/. -: ] ob) a
(</. -: < ob) 'a'
(</. -: < ob) ''

NB. integer
a=.?3 4 5 6$100
(</.  -: < ob) a
(</.  -: < ob) ,a
(]/.  -: ] ob) a
(+//. -: +/ ob) a
(</.  -: < ob) i.0 7
(</.  -: < ob) i.7 0 4
(</.  -: < ob) i.0 0 4 5
(</.  -: < ob) 34
(</.  -: < ob) 0$34

NB. floating point
a=.o._40+?10 5$100
(</.  -: < ob) a
(</.  -: < ob) ,a
(]/.  -: ] ob) a
(+//. -: +/ ob) a
(</.  -: < ob) _3.454
(</.  -: < ob) 0$_3.4

NB. complex
a=.^0j0.01*_400+?10 5$1000
(</.  -: < ob) a
(</.  -: < ob) ,a
(]/.  -: ] ob) a
(+//. -: +/ ob) a
(</.  -: < ob) _3j454
(</.  -: < ob) 0$_3j4

NB. boxed
t=.(1=?70$3)<;.1 ?70$100
a=.t{~?10 3$#t
(</. -: < ob) a
(</. -: < ob) ,a
(]/. -: ] ob) a
(</. -: < ob) <i.3 4
(</. -: < ob) 0$<'_3j4'


NB.  x f/. y ------------------------------------------------------------

NB. Boolean
a=.1=?11 5$2
k (</.   -: < key) a    [ k=.?11$4
k (]/.   -: ] key) a    [ k=.?11$4
k (+.//. -: +./ key) a  [ k=.?11$4

NB. literal
a=.a.{~32+?11$95
k (</. -: < key) a      [ k=.?11$4
k (]/. -: ] key) a      [ k=.?11$4

NB. integer
a=.?11 5$110
k (</.  -: < key) a     [ k=.?11$4
k (]/.  -: ] key) a     [ k=.?11$4
k (+//. -: +/ key) a    [ k=.?11$4

NB. floating point
a=.o._40+?11$110
k (</.  -: < key) a     [ k=.?11$4
k (]/.  -: ] key) a     [ k=.?11$4
k (+//. -: +/ key) a    [ k=.?11$4

NB. complex
a=.^0j0.01*_400+?11 5$1100
k (</.  -: < key) a     [ k=.?11$4
k (]/.  -: ] key) a     [ k=.?11$4
k (+//. -: +/ key) a    [ k=.?11$4

NB. boxed
a=.x{~?11 3$#x=.(1=?70$3)<;.1 ?70$110
k (</. -: < key) a      [ k=.?11$4
k (]/. -: ] key) a      [ k=.?11$4

''        -: '' </. ''
''        -: '' </. i.0 4 5
''        -: (i.0 4 5) </. ''
(,<,5)    -: 4 </. 5
(,<i.1 9) -: 4 </. i.1 9
(,<,4)    -: (i.1 9) </. 4
(,<,4)    -: (i.1 0) </. 4
(,<x )    -: (i.(#x),0) </. x=.'abcdefghij'
