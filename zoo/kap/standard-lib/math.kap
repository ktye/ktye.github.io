⍝ Implementation here:
⍝ https://gitlab.com/n9n/apl/-/commit/af428f5713b2ddd5c928806168316b6fb3b79132

namespace("kap")

∇ norm (B) {
  (B+.×+B)*0.5
}

∇ QR (B) {
  n←(⍴B)[1]
  if (n ≤ 1) {
    t ← norm ,B
    (B÷t) (⍪t)
  } else {
    m ← ⌈n÷2
    a0 ← ((1↑⍴B),m) ↑ B
    a1 ← (0,m) ↓ B
    (q0 r0) ← QR a0
    c ← (+⍉q0) +.× a1
    (q1 r1) ← QR a1-q0+.×c
    (q0,q1) ((r0,c)⍪((⌊n÷2),-n)↑r1)
  }
}

∇ Rinv (B) {
  n ← 1↑⍴B
  if (n = 1) {
    ÷B
  } else {
    m ← ⌈n÷2
    ai ← Rinv (m,m)↑B
    di ← Rinv (m,m)↓B
    b ← (m,m-n)↑B
    bx ← -ai+.×b+.×di
    (ai,bx)⍪((⌊n÷2),-n)↑di    
  }
}


∇ (A) ⌹ (B) {
  if (isLocallyBound('A)) {
    (⌹B)+.×A
  } else {
    rankB ← ↑⍴⍴B
    if (0 = rankB) {
      ÷B
    } else {
      if (1 = rankB) {
        ⌹ ⍪B
      } else {
        if ((2 ≠ rankB) ∨ (0∊≥/⍴B)) {
          →"error"
        }
        (Q R) ← QR B
        (Rinv R) +.× +⍉Q
      }
    }
  }
}

declare(:export ⌹)

∇ (A) ⊥ (B) {
  +/ (×\ ¯1↓1,(0×B)+⌽A) × ⌽B
}

declare(:export ⊥)

∇ (A) ⊤ (B) {
  declare(:local a0)
  declare(:local d)
  ⍉ ⌽ a0 (|⍤1) ⌊B ∘.÷ d←×\ ¯1 ↓ 1,a0←⌽A
}

declare(:export ⊤)
